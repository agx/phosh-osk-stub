if get_option('gtk_doc')

toml_data = configuration_data()
toml_data.set('VERSION', meson.project_version())

pos_toml = configure_file(
  input: 'phosh-osk-stub.toml.in',
  output: 'phosh-osk-stub.toml',
  configuration: toml_data
)

dependency('gi-docgen', version: '>= 2021.1',
           fallback: ['gi-docgen', 'dummy_dep'],
           native: true,
           required: get_option('gtk_doc'))

gidocgen = find_program('gi-docgen')

docs_dir = datadir / 'doc'

custom_target('phosh-osk-stub',
  input: [ pos_toml, pos_gir[0] ],
  output: 'pos-0',
  command: [
    gidocgen,
    'generate',
    '--quiet',
    '--add-include-path=@0@'.format(meson.current_build_dir() / '../../src'),
    '--config=@INPUT0@',
    '--output-dir=@OUTPUT@',
    '--no-namespace-dir',
    '--content-dir=@0@'.format(meson.current_source_dir()),
    '@INPUT1@',
  ],
  build_by_default: true,
  install: true,
  install_dir: docs_dir,
)

endif
